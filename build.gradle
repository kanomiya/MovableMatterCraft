
// For those who want the bleeding edge
buildscript {
	repositories {
		jcenter()
		maven {
			name = "forge"
			url = "http://files.minecraftforge.net/maven"
		}
	}
	dependencies {
		classpath 'net.minecraftforge.gradle:ForgeGradle:2.1-SNAPSHOT'
	}
}

apply plugin: 'net.minecraftforge.gradle.forge'

/*
// for people who want stable - not yet functional for MC 1.8.8 - we require the forgegradle 2.1 snapshot
plugins {
	id "net.minecraftforge.gradle.forge" version "2.0.2"
}
*/
version = "0.1.2"
group= "com.kanomiya.mcmod.movablemattercraft" // http://maven.apache.org/guides/mini/guide-naming-conventions.html
archivesBaseName = "movablemattercraft"
sourceCompatibility = 1.8
targetCompatibility = 1.8

minecraft {
	version = "1.9-12.16.0.1865-1.9"
	runDir = "run"

	mappings = "snapshot_20160416"

	tasks.withType(JavaCompile) {
		options.encoding = 'UTF-8'
		options.compilerArgs += ['-source', '1.8', '-target', '1.8']
	}

	replaceIn 'package-info.java'
	replace "@VERSION@", project.version
}

dependencies {

}

processResources {
	// this will ensure that this task is redone when the versions change.
	inputs.property "version", project.version
	inputs.property "mcversion", project.minecraft.version

	// replace stuff in mcmod.info, nothing else
	from(sourceSets.main.resources.srcDirs) {
		include 'mcmod.info'

		// replace version and mcversion
		expand 'version':project.version, 'mcversion':project.minecraft.version
	}


	// copy everything else, thats not the mcmod.info
	from(sourceSets.main.resources.srcDirs) {
		exclude 'mcmod.info'
	}

	from 'LICENSE'
}

jar {
	appendix = minecraft.version
}


task devJar(type: Jar, dependsOn: sourceJar) {
	appendix = minecraft.version
	classifier = "dev"

	from sourceSets.main.output.classesDir
	from sourceSets.main.output.resourcesDir
	from 'build/sources/main/java'
}

task apiJar(type: Jar, dependsOn: sourceJar) {
	appendix = minecraft.version
	classifier = "api"

	ext.apiPackage = "com/kanomiya/mcmod/movablemattercraft/api/"

	from (sourceSets.main.output) {
		include ext.apiPackage
	}

	from ('build/sources/main/java') {
		include ext.apiPackage
	}

	from 'LICENSE'
}

task apixJar(type: Jar, dependsOn: apiJar) {
	appendix = minecraft.version
	classifier = "apix"

	ext.apixPackage = "com/kanomiya/mcmod/movablemattercraft/apix/"

	from (sourceSets.main.output) {
		include ext.apixPackage
	}

	from ('build/sources/main/java') {
		include ext.apixPackage
	}

	from 'LICENSE'
}

artifacts {
	archives devJar, apiJar, apixJar
}

sourceJar.appendix = minecraft.version

